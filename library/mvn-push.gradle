apply plugin: 'maven'
apply plugin: 'signing'

def isReleaseBuild() {
    return android.defaultConfig.versionName.contains("SNAPSHOT") == false
}

def sonatypeRepositoryUrl() {
    if (isReleaseBuild()) {
        println 'RELEASE BUILD'
        return hasProperty('RELEASE_REPOSITORY_URL') ? RELEASE_REPOSITORY_URL : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
    } else {
        println 'SNAPSHOT BUILD'
        return hasProperty('SNAPSHOT_REPOSITORY_URL') ? SNAPSHOT_REPOSITORY_URL : "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

def getRepositoryUsername() {
    return hasProperty('nexusUsername') ? nexusUsername : ""
}

def getRepositoryPassword() {
    return hasProperty('nexusPassword') ? nexusPassword : ""
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                pom.groupId = 'jp.co.recruit_mp'
                pom.artifactId = 'android-HeaderFooterGridView'
                pom.version = android.defaultConfig.versionName

                repository(url: sonatypeRepositoryUrl()) {
                    authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
                }

                pom.project {
                    name 'android-HeaderFooterGridView'
                    packaging 'aar'
                    description 'HeaderFooterGridView supports adding header rows and footer rows to GridView for Android.'
                    url 'https://github.com/recruit-mp/android-HeaderFooterGridView'

                    scm {
                        url 'https://github.com/recruit-mp/android-HeaderFooterGridView.git'
                        connection 'scm:git@github.com:recruit-mp/android-HeaderFooterGridView.git'
                        developerConnection 'scm:git@github.com:recruit-mp/android-HeaderFooterGridView.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id "recruit_mp"
                            name 'Recruit Marketing PartnersCo.,Ltd.'
                        }
                    }
                }
            }
        }
    }

    signing {
        required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    task androidJavadocs(type: Javadoc) {
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        source = android.sourceSets.main.java.getSrcDirs()
        classpath = files(ext.androidJar)
        options.links("http://docs.oracle.com/javase/7/docs/api/")
    }

    task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
        classifier = 'javadoc'
        from androidJavadocs.destinationDir
    }

    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.getSrcDirs()
    }

    artifacts {
        archives androidSourcesJar
        archives androidJavadocsJar
    }
}
